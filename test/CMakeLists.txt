#==============================================================================
# nfx-stringutils - Test suite CMake configuration
#==============================================================================

#----------------------------------------------
# Test condition check
#----------------------------------------------

if(NOT NFX_STRINGUTILS_BUILD_TESTS)
    return()
endif()

#----------------------------------------------
# GoogleTest integration
#----------------------------------------------

include(GoogleTest)

#----------------------------------------------
# Parallel test configuration
#----------------------------------------------

include(ProcessorCount)
ProcessorCount(N)
if(NOT N EQUAL 0)
    set(CTEST_PARALLEL_LEVEL ${N})
    message(STATUS "Setting parallel test level to ${N} cores")
else()
    set(CTEST_PARALLEL_LEVEL 4)
    message(STATUS "Could not detect CPU count, setting parallel test level to 4")
endif()

#----------------------------------------------
# Tests source files
#----------------------------------------------

set(test_sources)

list(APPEND test_sources
    Tests_CharacterClassification.cpp
    Tests_CharacterRemoval.cpp
    Tests_EdgeCases.cpp
    Tests_EscapeUnescape.cpp
    Tests_FormatValidation.cpp
    Tests_NetworkValidation.cpp
    Tests_PredicateOperations.cpp
    Tests_StringComparison.cpp
    Tests_StringFormatting.cpp
    Tests_StringNumericParsing.cpp
    Tests_StringOperations.cpp
    Tests_StringSplitter.cpp
    Tests_StringValidation.cpp
    Tests_SubstringExtraction.cpp
    Tests_UrlEncoding.cpp
)

#----------------------------------------------
# Configure test executables
#----------------------------------------------

foreach(test_source ${test_sources})
    get_filename_component(test_target_name ${test_source} NAME_WE)

    if(NOT TARGET ${test_target_name})
        add_executable(${test_target_name} ${test_source})

        #----------------------------------------------
        # Target linking
        #----------------------------------------------

        target_link_libraries(${test_target_name}
        PRIVATE
            nfx-stringutils::nfx-stringutils
            GTest::gtest_main
        )

        #----------------------------------------------
        # Compiler warnings
        #----------------------------------------------

        target_compile_options(${test_target_name}
            PRIVATE
                $<$<CXX_COMPILER_ID:MSVC>:/W4 /WX>
                $<$<OR:$<CXX_COMPILER_ID:GNU>,$<CXX_COMPILER_ID:Clang>>:-Wall -Wextra -Werror>
        )

        #----------------------------------------------
        # Properties
        #----------------------------------------------

        set_target_properties(${test_target_name}
        PROPERTIES
            CXX_STANDARD 20
            CXX_STANDARD_REQUIRED ON
            CXX_EXTENSIONS OFF
            POSITION_INDEPENDENT_CODE ON
            DEBUG_POSTFIX "-d"
            RUNTIME_OUTPUT_DIRECTORY "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}"
            RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}"
            RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}"
        )

        #----------------------------------------------
        # Test discovery and registration
        #----------------------------------------------

        gtest_discover_tests(${test_target_name}
        WORKING_DIRECTORY "$<TARGET_FILE_DIR:${test_target_name}>"
        DISCOVERY_MODE POST_BUILD
        PROPERTIES
        TIMEOUT 120
        RUN_SERIAL OFF
        )
    endif()
endforeach()

